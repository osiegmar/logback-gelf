plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'com.github.spotbugs' version '1.6.9'
}

group = "de.siegmar"
archivesBaseName = "logback-gelf"
version = "2.1.2"

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    api 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.guava:guava:27.0.1-jre'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

artifacts {
    archives javadocJar, sourcesJar
}

checkstyle {
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    configProperties = [samedir: "${rootDir}/config/checkstyle", project_loc: "${rootDir}"]
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

if (hasProperty('ossrhUsername')) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "logback-gelf"

                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'Logback GELF'
                    description = 'Logback appender for sending GELF messages with zero additional dependencies.'
                    url = 'https://github.com/osiegmar/logback-gelf'
                    licenses {
                        license {
                            name = 'GNU Lesser General Public License version 2.1'
                            url = 'http://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                        }
                    }
                    scm {
                        url = 'https://github.com/osiegmar/logback-gelf'
                        connection = 'scm:git:https://github.com/osiegmar/logback-gelf.git'
                    }
                    developers {
                        developer {
                            id = 'osiegmar'
                            name = 'Oliver Siegmar'
                            email = 'oliver@siegmar.de'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$ossrhUsername"
                    password "$ossrhPassword"
                }
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
