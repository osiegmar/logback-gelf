plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'com.github.spotbugs' version '3.0.0'
}

group = "de.siegmar"
version = "3.0.0"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation 'com.google.guava:guava:28.1-jre'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.10.1'
    testImplementation 'org.bouncycastle:bcpkix-jdk15on:1.64'
}

test {
    useJUnitPlatform()
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

if (hasProperty('ossrhUsername')) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "logback-gelf"
                from components.java

                pom {
                    name = 'Logback GELF'
                    description = 'Logback appender for sending GELF messages with zero additional dependencies.'
                    url = 'https://github.com/osiegmar/logback-gelf'
                    licenses {
                        license {
                            name = 'GNU Lesser General Public License version 2.1'
                            url = 'http://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                        }
                    }
                    scm {
                        url = 'https://github.com/osiegmar/logback-gelf'
                        connection = 'scm:git:https://github.com/osiegmar/logback-gelf.git'
                    }
                    developers {
                        developer {
                            id = 'osiegmar'
                            name = 'Oliver Siegmar'
                            email = 'oliver@siegmar.de'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$ossrhUsername"
                    password "$ossrhPassword"
                }
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
