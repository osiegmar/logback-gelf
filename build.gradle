plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.5.0'
}

group = "de.siegmar"
version = "4.0.1"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'ch.qos.logback:logback-classic:1.2.8'
    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation 'com.google.guava:guava:31.0.1-jre'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    testImplementation 'org.bouncycastle:bcpkix-jdk15on:1.69'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.matching {task -> task.name.startsWith('spotbugs')}.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'de.siegmar.logbackgelf')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "logback-gelf"
            from components.java

            pom {
                name = 'Logback GELF'
                description = 'Logback appender for sending GELF messages with zero additional dependencies.'
                url = 'https://github.com/osiegmar/logback-gelf'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License version 2.1'
                        url = 'http://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                scm {
                    url = 'https://github.com/osiegmar/logback-gelf'
                    connection = 'scm:git:https://github.com/osiegmar/logback-gelf.git'
                }
                developers {
                    developer {
                        id = 'osiegmar'
                        name = 'Oliver Siegmar'
                        email = 'oliver@siegmar.de'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            credentials(PasswordCredentials)
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
